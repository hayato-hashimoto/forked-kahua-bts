;; -*- coding: euc-jp; mode: kahua -*-
;;
;;  Copyright (c) 2005 Kahua.Org, All rights reserved.
;;  See COPYING for terms and conditions of using this software
;;
;; $Id: hidden.kahua,v 1.5 2006/03/02 17:03:23 cut-sea Exp $
;;

;; analysis errorlog(hidden page)
;;
;;
(define (log->list log)
  (with-db (db *kagoiri-musume-log-database-name*)
      (list (sys-strftime "%Y/%m/%d %H:%M:%S"
			  (sys-localtime (ctime-of log)))
	    (case (category-of log)
	      ((Error) "エラー")
	      ((Trace) "トレース")
	      ((Warning) "警告")
	      ((Logging) "ログ")
	      (else (x->string (category-of log))))
	    (case (level-of log)
	      ((1) "緊急")
	      ((2) "重要")
	      ((3) "普通")
	      ((4) "軽微")
	      ((5) "無視")
	      (else (x->string (level-of log))))
	    (or (fan-of log) "-")
	    (list/ (map/ (lambda (v)
			   (dl/ (dt/ (x->string (car v)))
				(dd/ (x->string (cdr v)))))
			 (context-of log))
		   (pre/ (message-of log))))))

(define-entry (log-analysis)
  (permission-check-page
   (lambda (u)
     (if (kahua-user-has-role? u '(admin))
	 u #f))
   ($$ "システム管理者のアカウントが必要です")
   (h2/ ($$ "籠入娘。エラーログ調査(暫定)"))
   (let1 logs (filter (lambda (l)
			(date>=? (sys-time->date (ctime-of l))
				 (nth-day-before 10 (today))))
		      (with-db (db *kagoiri-musume-log-database-name*)
			  (make-kahua-collection <errorlog>)))
     (table/
      (@/ (class "listing"))
      (tr/ (map/ th/ '("日時" "種別" "レベル" "ファン" "内容")))
      (map/ (lambda (l)
	      (tr/ (map/ td/ (log->list l))))
	    (reverse logs))))))

;; メール送信
;;
(define-entry (sendmail)
  (permission-check-page
   (lambda (u)
     (if (kahua-user-has-role? u '(admin))
	 u #f))
   ($$ "システム管理者のアカウントが必要です")
   (h2/ "メール送信")
   (form/cont/
    (@@/ (cont (entry-lambda (:keyword to subject content)
		   (or (string-null? to)
		       (string-null? subject)
		       (string-null? content)
		       (let ((email (email-address-of (current-fan)))
			     (fan (fan-name-of (current-fan))))
			 (sendmail/headers content
					   :to to
					   :from #`",|fan| <,|email|>"
					   :reply-to email
					   :subject subject
					   :in-reply-to email)))
		 (redirect/cont (cont sendmail)))))
    (table/
     (tr/ (th/ "宛先:")
	  (td/ (input/ (@/ (type "text") (name "to")
			   (size 80) (id "focus")))))
     (tr/ (th/ "題名:")
	  (td/ (input/ (@/ (type "text") (name "subject")
			   (size 80) (id "focus")))))
     (tr/ (th/ "内容:")
	  (td/ (textarea/ (@/ (type "text") (name "content")
			      (rows 30) (cols 80) (id "focus"))))))
    (input/ (@/ (type "submit") (name "send") (value "送信"))))))

