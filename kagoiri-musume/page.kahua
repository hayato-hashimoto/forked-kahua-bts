;; -*- coding: euc-jp; mode: kahua -*-
;;
;;  Copyright (c) 2005 Kahua.Org, All rights reserved.
;;  See COPYING for terms and conditions of using this software
;;
;; $Id: page.kahua,v 1.30 2006/03/01 15:06:59 cut-sea Exp $
;;
(load "kagoiri-musume/util.kahua")
(load "kagoiri-musume/version.kahua")

(define-if-not-bound *kagoiri-musume-title*
  "籠入娘。 - Groupie System")

;
; page base
;
(define (page-base . body)
  (error-log-write 'Logging 5 "page-base logging.")
  (html/
   (head/ (title/ *kagoiri-musume-title*)
	  (meta/ (@/ (http-equiv "Content-type")
		     (content "text/html; charset=euc-jp")))
	  (link/ (@/ (rel "stylesheet") (type "text/css")
		     (href (kahua-static-document-url
			    "kagoiri-musume/kagoiri-musume.css"))))
          (with-ie/
           (link/ (@/ (rel "stylesheet") (type "text/css") (media "screen")
                      (href (kahua-static-document-url
                             "kagoiri-musume/fixed4ie.css"))))
           (script/ (@/ (type "text/javascript"))
                    "onload = function() { content.focus() }"))
          (script/ (@/ (type "text/javascript")
                       (src (kahua-static-document-url
                             "kagoiri-musume/prototype.js"))))
          (script/ (@/ (type "text/javascript"))
                   (format "var kahua_self_uri_full = '~a';" (kahua-self-uri-full))
                   (format "var kahua_loading_msg = '~a';" ($$ "読み込み中")))
          (script/ (@/ (type "text/javascript")
                       (src (kahua-static-document-url
                             "kagoiri-musume/async.js"))))
          (script/ (@/ (type "text/javascript")
                       (src (kahua-static-document-url
                             "kagoiri-musume/kagoiri-musume.js"))))
          )
   (body/
    (node-set body)
    (div/ (@/ (id "bottom-pane"))
	  (p/ #`"Copyright(C) 2005 Kahua Project    Kagoiri Musume ver ,*kagoiri-musume-version*")))))

;
; simple page for no permission check
;
(define (simple-page . body)
  (page-base
   (h1/ *kagoiri-musume-title*)
   (hr/)
   (node-set body)))

;
; kagoiri-musume-page-template
;
(define (kagoiri-page . body)
  (page-base
   ;; navi-link
   (div/ (@/ (id "header"))
         (h1/ (@/ (class "title")) (a/cont/ *kagoiri-musume-title*))
;;         (a/cont/
;;	  (@/ (class "clickable"))
;;          (@@/ (cont kagoiri)) ($$ "トップ"))
         (a/cont/
	  (@/ (class "clickable"))
          (@@/ (cont admin-system)) ($$ "システム管理"))
         (a/cont/
	  (@/ (class "clickable"))
          (@@/ (cont **)) ($$ "ユニット一覧"))
         (if (not (kahua-current-user))
             (a/cont/ (@/ (class "clickable"))
		      (@@/ (cont task-calendar))
                      ($$ "Login")) "")
         (if (and (kahua-current-user)
		  (not (equal? "guest" (ref (kahua-current-user) 'login-name))))
             (a/cont/
	      (@/ (class "clickable"))
              (@@/ (cont change-passwd)) ($$ "パスワード変更")) "")
         (if (kahua-current-user)
             (span/ ($$ " Now login:")
                  (a/cont/ (@/ (class "clickable"))
			   (@@/ (cont task-calendar))
                           (ref (kahua-current-user) 'login-name))) "")
         (if (kahua-current-user)
             (a/cont/
	      (@/ (class "clickable"))
              (@@/ (cont logout)) ($$ "Logout")) "")
         (if (kahua-current-user)
             (form/cont/ (@/ (class "searchbox"))
                         (@@/ (cont search))
                         (input/ (@/ (type "text") (name "word") (size 10)))
                         (input/ (@/ (type "submit") (value ($$ "検索"))))) "")
         )
   
   (div/ (@/ (id "body"))
   (node-set body))))

;
; permission check page
;
; [checker]
; checker is a predicate for <kahua-user> object.
; return value is #f or <kahua-user> object.
; So, at default, this template check whether kahua-user or not,
; therefore, you write additional check.
; If you care only kahua-user or not, you set identity as checker.
;
(define-syntax permission-check-page
  (syntax-rules ()
    ((_ checker msg body ...)
     (begin
       (or (and (kahua-current-user)
		(not (ref (kahua-current-user) 'inactive))
                (checker (kahua-current-user)))
           ;; new account set
           (set! (kahua-current-user)
               (ref (login-pc-page #f checker msg) 'login-name)))
       (kagoiri-page (node-set (list body ...)))))))


;
; maybe, redirect-page will called at the end of begin-body.
;
(define-syntax permission-check-logic
  (syntax-rules ()
    ((_ checker msg begin-body)
     (begin
       (or (and (kahua-current-user)
		(not (ref (kahua-current-user) 'inactive))
                (checker (kahua-current-user)))
           ;; new account set
           (set! (kahua-current-user)
               (ref (login-pc-page #f checker msg) 'login-name)))
       begin-body))))

;
; partial continuation login page
;
(define (login-pc-page cont checker message . errmsg)
  (let/pc k
      (if cont
	  (set! k cont))
    (kagoiri-page
     (h1/ ($$ "籠入娘。へようこそ！"))
     (h3/ message)
     (form/cont/
      (@@/ (cont (entry-lambda (:keyword name pass)
		     (let1 user (and-let* ((kuser (kahua-check-user name pass)))
				  (checker kuser))
		       (if user
			   (k user)
			   (login-pc-page k checker message))))))
      (table/
       (tr/ (th/ "Login Name")
	    (td/ (input/ (@/ (type "text") (name "name") (id "focus")
			     (value "")))))
       (tr/ (th/ "Password")
	    (td/ (input/ (@/ (type "password") (name "pass")
			     (value ""))))))
      (input/ (@/ (type "submit") (name "submit") (value "login")))
      (map/ (lambda (m)
	      (p/ (@/ (class "warning")) m))
	    errmsg)))))

(define-entry (logout)
  (error-log-write 'Logging 5 #`"logout")
  (set! (kahua-current-user) #f)
  (redirect-page))

;
; redirect page
;
(define (redirect-page . path)
  (let1 path (get-optional path "")
    (html/ (extra-header/
	    (@/ (name "Status") (value "302 Moved")))
	   (extra-header/
	    (@/ (name "Location")
		(value (string-append (kahua-self-uri-full) path)))))))


(define-method navigation/ ((unit <unit>))
  (div/ (@/ (id "navigation"))
        (a/cont/ ($$ "トップ"))
        " > "
        (span/ (@/ (class "current"))
              (link-of unit))))


(define-method navigation/ ((musume <musume>))
  (div/ (@/ (id "navigation"))
        (a/cont/ ($$ "トップ"))
        " > "
        (link-of (unit-of musume))
        " > "
        (span/ (@/ (class "current"))
              (link-of musume))))

(define-method navigation/ ()
  (div/ (@/ (id "navigation"))
        (span/ (@/ (class "current"))
               (a/cont/ ($$ "トップ")))))